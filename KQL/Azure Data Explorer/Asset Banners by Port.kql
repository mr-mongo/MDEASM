let workspace = '{_easm_workspace_name}';
let lastseen = 30d;
let oldestsnapshot = EasmAsset|where WorkspaceName == workspace|summarize by SnapshotDateTime|sort by SnapshotDateTime asc|take 1;
let port_num = replace_regex('{_easm_port_to_portlookup}', @'.+Port":(\d+).+', @'\1');
EasmAssetBanner
|where WorkspaceName == workspace
|join kind=anti oldestsnapshot on SnapshotDateTime
|where Port == port_num
|where BannerLastSeen > ago(lastseen)
|summarize hint.strategy=shuffle arg_max(SnapshotDateTime, *) by AssetName,ScanType,Port
|project-keep AssetName, Port, ScanType, Banner*
|project-reorder AssetName,Port,ScanType,Banner
|sort by AssetName {_easm_port_sort}