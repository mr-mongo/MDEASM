let workspace = '{_easm_workspace_name}';
let lastseen = ago(30d);
let oldestsnapshot = EasmAsset|where WorkspaceName == workspace|summarize by SnapshotDateTime|sort by SnapshotDateTime asc|take 1;
let cvssscore = {_easm_cvssscore};
let cvss3score = {_easm_cvss3score};
EasmAssetWebComponent
|where WorkspaceName == workspace
|join kind=anti oldestsnapshot on SnapshotDateTime
|where array_length(WebComponentCves) != 0
|where WebComponentLastSeen > lastseen
|mv-expand WebComponentCves
|extend Cve = tostring(WebComponentCves.Cve), Cwe = tostring(WebComponentCves.Cwe), CvssScore = tolong(WebComponentCves.CvssScore), Cvss3Score = toreal(WebComponentCves.Cvss3Score)
|where (CvssScore >= cvssscore or Cvss3Score >= cvss3score)
|summarize hint.strategy=shuffle arg_max(SnapshotDateTime, *) by AssetUuid,WebComponentName,WebComponentVersion,Cve
|project-keep WebComponentCategory,WebComponentName,WebComponentVersion,Cve,Cwe,CvssScore,Cvss3Score,AssetName,WebComponentLastSeen
|project-reorder AssetName,WebComponentName,WebComponentVersion,Cve,CvssScore,Cvss3Score,Cwe
|sort by WebComponentLastSeen desc
//|summarize Asset = make_list(pack_all()) by AssetName
//|summarize CVE = make_list(pack_all()) by Cve
//|summarize WebComponent = make_list(pack_all()) by WebComponentName,WebComponentVersion